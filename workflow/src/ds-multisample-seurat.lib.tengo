ll := import("@platforma-sdk/workflow-tengo:ll")
pt := import("@platforma-sdk/workflow-tengo:pt")
render := import("@platforma-sdk/workflow-tengo:render")
assets := import("@platforma-sdk/workflow-tengo:assets")
pColumn := import("@platforma-sdk/workflow-tengo:pframes.pcolumn")
parseSeuratTpl := assets.importTemplate(":parse-multisample-seurat")
parseCsvTpl := assets.importTemplate(":parser-csv")

util := import(":util")

export {
    isGrouped: true,

    getColumns: func(fImport, importFile) {
        seuratFile := importFile(fImport)
        columnsContent := render.create(parseSeuratTpl, {
            seuratFile: seuratFile,
            sampleColumnName:  "sample"
        }).output("columnsContent")
        columnsResult := render.create(parseCsvTpl, {
            data: columnsContent
        }).output("content")
        return columnsResult
    },

    getSamples: func(dataset, importFile) {
        result := {}
        sampleColumnName := dataset.content.sampleColumnName
        if !sampleColumnName {
            sampleColumnName = "sample"
        }
        for groupId, importHandle in dataset.content.data {
            if !importHandle {
                ll.panic("File handle not set for group %v", groupId)
            }
            seuratFile := importFile(importHandle)
            samplesContent := render.create(parseSeuratTpl, {
                seuratFile: seuratFile,
                sampleColumnName: sampleColumnName
            }).output("samplesContent")
            samples := render.create(parseCsvTpl, {
                data: samplesContent
            }).output("content")
            result[groupId] = samples
        }
        return result
    },
    
    createDataset: func(blockId, sampleIdAxis, groupIdAxis, dataset, importFile) {
        
        extension := "rds"
        
        spec := util.datasetColumnSpecBase(blockId, dataset, extension)
        spec.axesSpec = [groupIdAxis]

        data := pColumn.resourceMapBuilder(1)
        for groupId, importHandle in dataset.content.data {
            if !importHandle {
                ll.panic("File handle not set for sample %v", groupId)
            }
            data.add([groupId], importFile(importHandle))
        }

        result := {}
        result["dataset." + dataset.id] = {
            spec: spec,
            data: data.build()
        }

        return result
    }
}


