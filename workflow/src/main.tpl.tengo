wf := import("@platforma-sdk/workflow-tengo:workflow")

maps := import("@platforma-sdk/workflow-tengo:maps")
ll := import("@platforma-sdk/workflow-tengo:ll")
smart := import("@platforma-sdk/workflow-tengo:smart")
file := import("@platforma-sdk/workflow-tengo:file")
pSpec := import("@platforma-sdk/workflow-tengo:pframes.spec")

json := import("json")

_P_COLUMN_DATA_RESOURCE_MAP := { Name: "PColumnData/ResourceMap", Version: "1" }
_P_COLUMN_DATA_JSON := { Name: "PColumnData/Json", Version: "1" }

// TODO implement
normalizeString := func(str) {
	return str
}

mapToPValueData := func(map) {
	data := {}
	for key, value in map {
		data[json.encode([key])] = value
	}
	result := {
		keyLength: 1,
		data: data
	}
	return result
}

createJsonPColumnData := func(data) {
	return smart.createValueResource(_P_COLUMN_DATA_JSON, data)
}

wf.body(func(args) {

	blockId := wf.blockId().getDataAsJson()

	sampleIdAxisSpec := {
		type: "String",
		name: "pl7.app/sampleId",
		annotations: {
			"pl7.app/label": "Sample"
		},
		domain: {
			"pl7.app/blockId": blockId
		}
	}

	exports := {}

	//
	// Creating metadata columns
	//

	for _, column in args.metadata {
		exports["metadata." + column.id] = {
			spec: {
				kind: "PColumn",
				name: "pl7.app/metadata",
				valueType: column.valueType,
				annotations: {
					"pl7.app/label": column.label
				},
				domain: {
					"pl7.app/columnId": column.global ? normalizeString(column.label) : column.id
				},
				axesSpec: [sampleIdAxisSpec]
			},
			data: createJsonPColumnData(json.encode(mapToPValueData(column.data)))
		}
	}

	//
	// Creating sample label column
	//

	exports["metadata.sampleId"] = {
		spec: {
			kind: "PColumn",
			name: "pl7.app/label",
			valueType: "String",
			annotations: {
				"pl7.app/label": args.sampleLabelColumnLabel,
				"pl7.app/isLabel": "true"
			},
			axesSpec: [sampleIdAxisSpec]
		},
		data: createJsonPColumnData(json.encode(mapToPValueData(args.sampleLabels)))
	}

	//
	// Creating datasets
	//

	fileImports := {}

	importFile := func(importHandle) {
		fromMap := fileImports[importHandle]
		if fromMap == undefined {
			fImport := file.importFile(importHandle)
			fileImports[importHandle] = fImport
			return fImport.file
		} else {
			return fromMap.file
		}
	}

	blockTitle := args.blockTitle
	if is_undefined(blockTitle) {
		blockTitle = "Samples & Data"
	}

	for _, dataset in args.datasets {
		exportKey := "dataset." + dataset.id

		trace := pSpec.makeTrace(undefined,
			{type: "milaboratories.samples-and-data", id: blockId, importance: 10, label: blockTitle},
			{type: "milaboratories.samples-and-data/dataset", id: dataset.id, importance: 100, label: dataset.label}
		)

		columnSpecParent := trace.inject({
			kind: "PColumn",
			name: "pl7.app/sequencing/data",
			domain: {
				"pl7.app/fileExtension": dataset.content.gzipped ? "fasta.gz" : "fasta",
				"pl7.app/block": blockId,
				"pl7.app/dataset": dataset.id
			},
			valueType: "File",
			annotations: {
				"pl7.app/label": "Sequencing Data",
				"pl7.app/hideDataFromUi": "true",
				"pl7.app/axisKeys/0": string(json.encode(maps.getKeys(dataset.content.data))) // axis values for sampleIdAxis
			}
		})

		if dataset.content.type == "Fasta" {
			columnSpec := maps.deepMerge(columnSpecParent, {
				domain: {
					"pl7.app/fileExtension": dataset.content.gzipped ? "fasta.gz" : "fasta"
				},
				axesSpec: [sampleIdAxisSpec]
			})

			data := smart.structBuilder(_P_COLUMN_DATA_RESOURCE_MAP, json.encode({
				keyLength: 1
			}))

			for sampleId, importHandle in dataset.content.data {
				if !importHandle {
					ll.panic("File handle not set for sample %v", sampleId)
				}
				data.createInputField(json.encode([sampleId])).set(importFile(importHandle))
			}

			exports[exportKey] = {
				spec: columnSpec,
				data: data.lockAndBuild()
			}

			break
		}

		// Code below service Fastq and MultilaneFastq datasets

		readIndexAxisSpec := {
			type: "String",
			name: "pl7.app/sequencing/readIndex",
			annotations: {
				"pl7.app/label": "Read Index"
			},
			domain: {
				// Actually this is a special case of a more generic notion of
				// a "dense" axis with specified key value list.
				// When this concept will be fully defined, the domain most
				// probably should be renamed to "pl7.app/dense"
				"pl7.app/readIndices": string(json.encode(dataset.content.readIndices))
			}
		}

		laneAxisSpec := {
			type: "String",
			name: "pl7.app/sequencing/lane",
			annotations: {
				"pl7.app/label": "Lane"
			}
		}

		fastqDataSpec := maps.deepMerge(columnSpecParent, {
			domain: {
				"pl7.app/fileExtension": dataset.content.gzipped ? "fastq.gz" : "fastq"
			}
		})

		if dataset.content.type == "Fastq" {
			fastqDataSpec.axesSpec = [sampleIdAxisSpec, readIndexAxisSpec]

			data := smart.structBuilder(_P_COLUMN_DATA_RESOURCE_MAP, json.encode({
				keyLength: 2
			}))

			for sampleId, fileGroup in dataset.content.data {
				for _, readIndex in dataset.content.readIndices {
					importHandle := fileGroup[readIndex]
					if !importHandle {
						ll.panic("File handle not set for %v in sample %v", readIndex, sampleId)
					}
					data.createInputField(json.encode([sampleId, readIndex])).set(importFile(importHandle))
				}
			}

			exports[exportKey] = {
				spec: fastqDataSpec,
				data: data.lockAndBuild()
			}
		} else if dataset.content.type == "MultilaneFastq" {
			fastqDataSpec.axesSpec = [sampleIdAxisSpec, laneAxisSpec, readIndexAxisSpec]

			data := smart.structBuilder(_P_COLUMN_DATA_RESOURCE_MAP, json.encode({
				keyLength: 3
			}))

			for sampleId, laneData in dataset.content.data {
				for laneId, fileGroup in laneData {
					for _, readIndex in dataset.content.readIndices {
						importHandle := fileGroup[readIndex]
						if !importHandle {
							ll.panic("File handle not set for %v, lane %v in sample %v", readIndex, laneId, sampleId)
						}
						data.createInputField(json.encode([sampleId, laneId, readIndex])).set(importFile(importHandle))
					}
				}
			}

			exports[exportKey] = {
				spec: fastqDataSpec,
				data: data.lockAndBuild()
			}
		} else if dataset.content.type == "TaggedFastq" {
			axesSpec := [sampleIdAxisSpec]
			for tag in dataset.content.tags {
				axesSpec = append(axesSpec, {
					type: "String",
					name: "pl7.app/sequencing/tag",
					domain: {
						"pl7.app/sequencing/tag": tag
					},
					annotations: {
						"pl7.app/label": tag + " tag"
					}
				})
			}
			if dataset.content.hasLanes {
				axesSpec = append(axesSpec, laneAxisSpec)
			}
			axesSpec = append(axesSpec, readIndexAxisSpec)
			fastqDataSpec.axesSpec = axesSpec

			data := smart.structBuilder(_P_COLUMN_DATA_RESOURCE_MAP, json.encode({
				keyLength: len(axesSpec)
			}))

			for sampleId, records in dataset.content.data {
				for record in records {
					keyPrefix := [sampleId]
					for tag in dataset.content.tags {
						tagValue := record.tags[tag]
						if !tagValue {
							ll.panic("No tag value for %v in sample %v", tag, sampleId)
						}
						keyPrefix = append(keyPrefix, tagValue)
					}
					if dataset.content.hasLanes {
						keyPrefix = append(keyPrefix, record.lane)
					}
					for readIndex in dataset.content.readIndices {
						importHandle := record.files[readIndex]
						key := maps.clone(keyPrefix)
						key = append(key, readIndex)
						key = json.encode(key)
						if !importHandle {
							ll.panic("File handle not set for key %v", key)
						}
						data.createInputField(key).set(importFile(importHandle))
					}
				}
			}

			exports[exportKey] = {
				spec: fastqDataSpec,
				data: data.lockAndBuild()
			}
		} else {
			ll.panic("Unknow dataset type %v", dataset.content.type)
		}
	}

	return {
		outputs: {
			fileImports: smart.createMapResource(maps.mapValues(fileImports, func(im) {
				return im.handle
			}))
		},
		exports: exports
	}
})
